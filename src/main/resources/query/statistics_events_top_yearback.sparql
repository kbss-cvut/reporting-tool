PREFIX rt: <http://onto.fel.cvut.cz/ontologies/reporting-tool/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX aso: <http://onto.fel.cvut.cz/ontologies/aviation-safety/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

PREFIX er: <http://dev.inbas.cz/rdf4j-server/repositories/>

SELECT ?event_type ?event_type_iri ?year ?month (COUNT(*) AS ?count)
{
  ?event
    rt:has_event_type ?event_type_iri ;
    aso:has_start_time ?start_time
  .

  FILTER NOT EXISTS {
     ?event a aso:Occurrence .
  }

  {
    SELECT DISTINCT ?event_type_iri {
      ?event_2 rt:has_event_type ?event_type_iri .
      FILTER NOT EXISTS {
     	  ?event_3 <http://onto.fel.cvut.cz/ontologies/aviation-safety/has_factor> ?event_2
      }
    }
  }

  BIND(year(now()) AS ?thisYear)
  BIND(month(now()) AS ?thisMonth)
  BIND(year(?start_time) AS ?year)
  BIND(month(?start_time) AS ?month)

  OPTIONAL {
    SERVICE er:eccairs-aviation-3.4.0.2 {
      ?event_type_iri rdfs:label ?event_type.
    }
  }
  OPTIONAL {
    SERVICE er:eccairs-aviation-1.3.0.8 {
      ?event_type_iri rdfs:label ?event_type.
    }
  }
  OPTIONAL {
    SERVICE er:inbas-model-2016-01-21:22-46-17 {
      ?event_type_iri rdfs:label ?event_type.
    }
  }
#   BIND(?event_type_iri AS ?event_type)
}
GROUP BY ?year ?month ?event_type ?event_type_iri
ORDER BY ?year ?month desc(?count) asc(?event_type)